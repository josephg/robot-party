@defaults.local = true

container = $("<div id='message-container' />")
messages = $("<div id='messages' />")
container.append messages

escapeHTML = (html) ->
  escaped =
    '&': '&amp;'
    '<': '&lt;'
    '>': '&gt;'
    '"': '&quot;'
  
  String(html).replace /[&<>]/g, (chr) -> escaped[chr]


@transmit "want layout", {id: 'message-container', name: "messages"}, ({data:{parent}}) ->
  
  @transmit "add style", "
    #message-container {
      overflow-y: scroll;
      width: 100%;
      height: 100%;
    }
    #messages {
      display: table;
    }
    .message {
      font-family: monospace
      display: table-row;
    }
    .message .source {
      display: table-cell;
      text-align: right;
      padding: 0 10px;
      border-right: 1px solid lightgray;
      min-width: 125px;
    }
    .message .text {
      display: table-cell;
      padding: 0 10px;
      word-break: break-all;
    }
    .message.json .text {
      color: blue;
    }
    "

  $(parent).append container


  @listen (msg) ->
    from = msg.user || msg.robot || "anonymous"
    if msg.type == 'chat'
      content = msg.data
      klass = "plain"
    else
      return unless !msg.local or @deluge
      content = JSON.stringify msg
      klass = "json"

    message = "<div class='message #{klass}'><div class='source'>#{from}</div><div class='text'>#{escapeHTML content}</div></div>"

    scroll = (container.scrollTop() + container.height() == container[0].scrollHeight)

    messages.append message
    
    container.scrollTop container[0].scrollHeight if scroll
  
  @listen "usercmd", ({data}) ->
    @deluge = true if data == "deluge on"
    @deluge = false if data == "deluge off"
