@robot "persistron",
  local: true
  version: "0.0.0"
  description: "Robot for storing things"

levelup = window?.indexedup or require('levelup')

# Not a default for indexedup
opts =
  createIfMissing: true
  errorIfExists: false

levelup './robots', opts, (err, db) =>
  return @transmit "error", err.message if err

  @listen type: "create value", local: true, ({data:{key, value}}, reply) ->
    db.get key, (err) =>
      if err
        console.log "writing key", key, "value", JSON.stringify(value)
        db.put key, JSON.stringify(value), (err) =>
          if err
            reply "error", err.message
          else
            reply "ok"
      else
        reply "error", "value already exists"

  @listen type: "set value", local: true, ({data:{key, value}}, reply) ->
    db.put key, JSON.stringify(value), (err) =>
      if err
        reply "error", err.message
      else
        reply "ok" 

  #@listen type: "clear values", local: true, (_, reply) ->
    #localStorage.clear()
    #reply "ok"

  @listen type: "get value", local: true, ({data: key}, reply) ->
    db.get key, (err, value) =>
      if err
        reply "error", err.message
      else
        reply "value is", JSON.parse value
