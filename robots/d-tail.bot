@robot 'd-tail',
  local: true
  version: '0.0.0'
  description: 'Detail view for robots, including list of children and edit/stop buttons.'


@listen type: 'robot clicked', local: true, ({data: id}) ->
  @transmit "get robot info", id, ({type, data: robot}) ->
    detailView robot if type isnt "error"

@listen type: 'message clicked', local: true, ({data: {from: id}}) ->
  return unless id
  @transmit "get robot info", id, ({type, data: robot}) ->
    detailView robot if type isnt "error"

@transmit "add style", "
  #detailpanel {
    width: 100%;
    height: 40%;
    border-bottom: 1px solid black;
    overflow-y: scroll;
  }
  #parent {
    width: 100%;
    height: 80px;
  }
"

escapeHTML = (html) -> $('<div/>').text(html).html() if html?

detailView = (robot) =>
  console.log "detail view got", robot
  $('#detailpanel').html "<div>#{escapeHTML robot.name}</div>
    <div>#{escapeHTML (robot.info.description or '')}</div>
    <button id='edit'>edit</button>
    <div>Parent</div>
    <div id='parent'></div>
    <div>Children</div>
    <div id='children'></div>"
  
  @transmit "get robot info", robot.parent, ({type, data: parent}) ->
    @transmit "render robots", target: '#parent', robots: [parent]

  @transmit "query robots", {parent: robot.id}, ({type, data: children}) ->
    @transmit "render robots", target: '#children', robots: children

  $('#edit').click =>
    @transmit to: robot.parent, local: false, type: "get robot", data: robot.id, ({type, data: code}) ->
      if type isnt "error"
        @transmit "edit robot", title: robot.name, text: code, id: robot.id
  

@listen "editbox saved", (data:{id, text: code}) ->
  @transmit "get robot info", id, ({type, data: robot}) ->
    if type isnt "error"
      @transmit to: robot.parent, type: "replace robot", trusted: true, local: false, data: {id, code}, ({type}) ->
        if type isnt "error"
          @transmit "robot saved", id
