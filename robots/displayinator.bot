@robot "displayinator",
  local: true
  description: "Main message window."

container = $("<div id='message-container' />")
messages = $("<div id='messages' />")
container.append messages

@transmit "add style", "
  #message-container {
    overflow-y: scroll;
    width: 100%;
    height: 100%;
  }
  #messages {
    display: table;
  }
  .message {
    display: table-row;
  }
"

@transmit "want layout", {id: 'message-container', name: "messages"}, ({data:{parent}}) ->
  $(parent).append container

  messages.on 'click', '.message', (ev) =>
    if id = $(ev.currentTarget).attr 'id'
      id = id.replace /^msg-/, ''
      @transmit 'message clicked', id
    else
      @transmit 'message clicked'
    ev.stopPropagation()

  container.click (ev) =>
    @transmit 'message clicked'

  @listen "display message", ({user, data: {message, id, re}}) ->
    scroll = (container.scrollTop() + container.height() == container[0].scrollHeight)

    id = if id then "msg-#{id}" else ""
    re = if re then "msg-#{re}" else ""

    html = $("<div id='#{id}' class='message'>#{message}</div>")

    sel = "##{re}, ##{re} ~ [data-re=\"#{re}\"]"

    if re and (reEl = $(sel).last()) and reEl.length
      console.log "appending after", re
      html.attr 'data-re', re
      reEl.after html
    else
      messages.append html
    
    container.scrollTop container[0].scrollHeight if scroll
  
